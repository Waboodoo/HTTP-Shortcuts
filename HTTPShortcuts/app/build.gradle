apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.bugsnag.android.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    kotlinOptions {
        languageVersion = '1.4'
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }

    defaultConfig {
        applicationId 'ch.rmy.android.http_shortcuts'
        minSdkVersion 16
        targetSdkVersion 30
        versionName '2.0.0'
        // 11,(2 digits major),(2 digits minor),(2 digits patch),(2 digits build)
        versionCode 1102010000
        buildConfigField 'String', 'BUGSNAG_API_KEY', "\"${rootProject.ext.bugsnagAPIKey}\""
        buildConfigField 'String', 'BUILD_TIMESTAMP', "\"${rootProject.ext.buildTimestamp}\""
        multiDexEnabled true

        manifestPlaceholders = [bugsnagAPIKey:rootProject.ext.bugsnagAPIKey]

        testInstrumentationRunnerArguments = [package: "ch.rmy.android.http_shortcuts"]

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        releaseWithCrashLogging {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "app-" + (output.getFilter(com.android.build.OutputFile.ABI) ?: "universal") + "-release.apk"
                }
            }
        }
    }

    dexOptions {
        jumboMode true
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding = false
        dataBinding = false
        aidl = false
        renderScript = false
        resValues = false
        shaders = false
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

if (!rootProject.ext.bugsnagAPIKey) {
    bugsnag {
        autoUpload false
    }
}

project.configure(project.android, {
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        debug.java.srcDirs += 'src/withoutCrashLogging/kotlin'
        release.java.srcDirs += 'src/withoutCrashLogging/kotlin'
        releaseWithCrashLogging.java.srcDirs += 'src/withCrashLogging/kotlin'
    }
})

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
    google()
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    /* Support libraries */
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'com.google.android.material:material:1.3.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:viewmodel:1.1.1'

    /* UI */
    implementation 'com.satsuware.lib:usefulviews:2.3.1'

    /* Dialogs / Activities */
    implementation('com.afollestad.material-dialogs:core:3.1.1') {
        transitive = true
    }
    implementation 'com.afollestad.material-dialogs:input:3.1.1'
    implementation 'me.priyesh:chroma:1.0.2'

    /* Animated progress bar */
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'

    /* Image selection */
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    /* Image display */
    implementation 'com.squareup.picasso:picasso:2.71828'

    /* HTTP & Network */
    implementation 'com.squareup.okhttp3:okhttp:3.12.1' // TODO: Use version 4 once dropping support for Kitkat and below
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    implementation 'com.burgstaller:okhttp-digest:1.12' // Can't update to latest version because that depends on okhttp v4
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    /* Tasker integration */
    implementation 'com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3'

    /* Scripting */
    implementation 'com.github.LiquidPlayer:LiquidCore:0.6.2'

    /* Password hashing */
    implementation 'org.mindrot:jbcrypt:0.4'

    /* Crash Reporting */
    releaseWithCrashLoggingImplementation 'com.bugsnag:bugsnag-android:5.2.0'

    /* cURL import & export */
    implementation project(path: ':curl_command')

    /* JSON serialization & deserialization */
    implementation 'com.google.code.gson:gson:2.8.6'

    /* RX */
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    /* Testing */
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.robolectric:shadows-multidex:$robolectricVersion"
    testImplementation 'androidx.test:monitor:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
    androidTestImplementation "junit:junit:$junitVersion"
}

apply plugin: 'realm-android'
